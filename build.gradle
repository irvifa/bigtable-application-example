buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
        jcenter()
    }
    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:2.0.2'
    }
}

plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.3.72'
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'kotlin'
apply plugin: 'com.github.johnrengelman.shadow'

group 'com.github.irvifa.meetups.cloud'
version '1.0-SNAPSHOT'

configurations {
    ktlint
}

project.ext {
    mainClass = 'com.github.irvifa.meetups.cloud.BigtableExampleApplication'
}

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    ktlint "com.github.shyiko:ktlint:0.29.0"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    implementation "io.dropwizard:dropwizard-core:$dropwizardVersion"
    implementation "io.dropwizard:dropwizard-json-logging:$dropwizardVersion"
    implementation "com.fasterxml.jackson.module:jackson-module-kotlin:2.9.6"
    implementation "com.smoketurner:dropwizard-swagger:1.3.1-1"
    implementation "com.google.code.gson:gson:$gsonVersion"
    implementation "org.slf4j:slf4j-api:$slf4jVersion"
    implementation "com.google.cloud.bigtable:bigtable-hbase-1.x:$bigtableVersion"
    testImplementation group: 'org.assertj', name: 'assertj-core', version: '3.9.1'
    testImplementation "io.dropwizard:dropwizard-testing:$dropwizardVersion"
    testCompile group: 'junit', name: 'junit', version: '4.12'
    testImplementation "io.mockk:mockk:1.9.1"
    testImplementation "org.junit.jupiter:junit-jupiter-api:$junitJupiterVersion"
}

project.shadowJar.dependsOn test

shadowJar {
    classifier = null
    mergeServiceFiles()
    exclude 'META-INF/*.DSA', 'META-INF/*.RSA', 'META-INF/*.SF'
    manifest {
        attributes 'Main-Class': mainClass
    }
}

task ktlint(type: JavaExec, group: "verification") {
    description = "Check Kotlin code style."
    classpath = configurations.ktlint
    main = "com.github.shyiko.ktlint.Main"
    args "src/**/*.kt"
}
check.dependsOn ktlint

task ktlintFormat(type: JavaExec, group: "formatting") {
    description = "Fix Kotlin code style deviations."
    classpath = configurations.ktlint
    main = "com.github.shyiko.ktlint.Main"
    args "-F", "src/**/*.kt"
}
